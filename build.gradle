plugins {
    id 'java'
    id 'application'
    id 'org.springframework.boot' version '3.3.4'
}

apply plugin: 'io.spring.dependency-management'

application {
    mainClassName = 'com.naeayedea.keith.KeithLauncher'
}

version '4.0.0'

def jacksonVersion = "2.17.2"

repositories {
    mavenCentral()
}

tasks.register('addProjectInformation') {
    group = "other"
    description = "Adds project information to JAR"

    //override some config so the local deployment runs better
    doFirst {
        ant.propertyfile(file: "./build/resources/main/application.properties") {
            entry( key: "keith.version", value: version)
            entry( key: "keith.project.name", value: rootProject.name)
        }
    }
}

processResources {
    finalizedBy addProjectInformation
}

dependencies {
    //spring
    implementation ("org.springframework.boot:spring-boot-starter") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    //replace logback with log4j2
    implementation "org.springframework.boot:spring-boot-starter-log4j2"

    //jackson
    implementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"

    //database
    implementation "org.mariadb.jdbc:mariadb-java-client:3.4.1"

    //discord related
    implementation "com.github.ygimenez:Pagination-Utils:4.0.6"
    implementation "net.dv8tion:JDA:5.1.1"
}

compileJava.options.encoding = 'UTF-8'

